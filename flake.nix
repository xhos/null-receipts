{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    git-hooks.url = "github:cachix/git-hooks.nix";
    git-hooks.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = {
    self,
    nixpkgs,
    git-hooks,
  }: let
    forAllSystems = f:
      nixpkgs.lib.genAttrs
      ["x86_64-linux" "aarch64-linux" "x86_64-darwin" "aarch64-darwin"]
      (system: f nixpkgs.legacyPackages.${system});
  in {
    checks = forAllSystems (pkgs: {
      pre-commit = git-hooks.lib.${pkgs.system}.run {
        src = ./.;
        hooks = {
          gotest.enable = true;
          govet.enable = true;
          alejandra.enable = true;
          golangci-lint = {
            enable = true;
            name = "golangci-lint";
            entry = "${pkgs.golangci-lint}/bin/golangci-lint fmt";
            types = ["go"];
          };

          nix-build = {
            enable = true;
            name = "nix-build";
            entry = pkgs.lib.getExe (pkgs.writeShellApplication {
              name = "nix-build-check";
              runtimeInputs = [pkgs.nix];
              text = "nix build --no-link";
            });
            stages = ["pre-push"];
            pass_filenames = false;
            files = "go\\.(mod|sum)|flake\\.nix";
          };
        };
      };
    });

    packages = forAllSystems (pkgs: {
      default = pkgs.buildGoModule {
        pname = "null-receipts";
        version = self.shortRev or self.dirtyShortRev or "dev";
        src = ./.;
        vendorHash = "sha256-GV1DKwZjFrY39GaWtF3JHSg9M/5dSbREB9JC9tOHylw=";
        subPackages = ["cmd/server"];
      };
    });

    devShells = forAllSystems (pkgs: {
      default = pkgs.mkShell {
        packages = with pkgs; [
          ollama

          go

          buf

          protoc-gen-go-grpc
          protoc-gen-connect-go
          protoc-gen-go

          (writeShellScriptBin "run" ''
            go run cmd/server/main.go
          '')

          (writeShellScriptBin "bump-protos" ''
            git -C proto fetch origin
            git -C proto checkout main
            git -C proto pull --ff-only
            git add proto
            git commit -m "chore: bump proto files"
            git push
          '')

          (writeShellScriptBin "regen" ''
            rm -rf internal/gen/
            ${buf}/bin/buf generate
          '')
        ];

        env.OLLAMA_MODELS = "./models";

        shellHook = "${self.checks.${pkgs.system}.pre-commit.shellHook}";
      };
    });

    formatter = forAllSystems (pkgs: pkgs.alejandra);
  };
}
